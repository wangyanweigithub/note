 

事件驱动的Web框架

里奥·佐维奇（Leo Zovic）

Leo（在网上更被称为inaimathi）是一位正在复苏的图形设计师，他专业编写Scheme，Common Lisp，
Erlang，Javascript，Haskell，Clojure，Go，Python，PHP和C。他目前在博客中涉及编程，玩棋盘游戏并
在以下网站工作安大略省多伦多市一家基于Ruby的初创公司。

2013年，我决定为纸牌和棋盘游戏编写名为House 的基于网络的游戏原型开发工具。在这些类型的游戏中，
一个玩家通常等待另一个玩家出手。但是，当其他玩家最终采取行动时，我们希望此后迅速将正在移动的玩
家通知给正在等待的玩家。

这个问题比最初看起来要复杂得多。在本章中，我们将探讨使用HTTP建立此类交互的问题，然后在Common
Lisp中构建一个Web框架，该框架可让我们将来解决类似的问题。

HTTP服务器的基础

在最简单的级别上，HTTP交换是单个请求，然后是单个响应。甲客户发送一个请求，该请求包括资源标识符
，HTTP版本标记，一些报头和一些参数。该服务器解析该请求，计算出怎样做它，并发送包含同样的HTTP版
本标签，响应代码，一些头和响应正文的响应。

注意，在此描述中，服务器响应来自特定客户端的请求。在我们的案例中，我们希望每位玩家在发生任何动
作时就立即进行更新，而不是仅在做出自己的动作时得到通知。这意味着我们需要服务器在不首先接收信息
请求的情况下将消息推送给客户端。^1个

有几种标准方法可通过HTTP启用服务器推送。

彗星/长轮询

“长时间轮询”技术使客户端在收到响应后立即向服务器发送新请求。服务器没有立即满足该请求，而是等待
后续事件做出响应。这有点语义上的区别，因为客户端仍在每次更新时都采取措施。

服务器发送的事件（SSE）

服务器发送的事件要求客户端启动连接，然后保持连接打开。服务器会定期将新数据写入连接而不会关闭该
连接，并且客户端会在传入新消息到达时解释它们，而不是等待响应连接终止。这比Comet / long poll方
法更有效，因为每条消息不必承担新HTTP标头的开销。

Web套接字

WebSockets是建立在HTTP之上的通信协议。服务器和客户端打开HTTP对话，然后执行握手和协议升级。最终
结果是他们仍在通过TCP / IP进行通信，但根本没有使用HTTP进行通信。与SSE相比，它的优势在于您可以
自定义协议以提高效率。

长期连接

这三种方法彼此完全不同，但是它们都有一个重要的特征：它们都依赖于长期存在的连接。长轮询取决于服
务器在新数据可用之前一直保持请求，SSE在客户端和服务器之间保持开放的流，并定期向其写入数据，
WebSocket更改特定连接正在使用的协议，但保持打开状态。

要了解为什么这可能会给您的普通HTTP服务器造成问题，让我们考虑一下底层实现的工作方式。

传统的HTTP服务器架构

 

单个HTTP服务器可同时处理许多请求。从历史上看，许多HTTP服务器都使用每个请求线程的体系结构。也就
是说，对于每个传入的请求，服务器都会创建一个线程来执行响应所需的工作。

由于这些连接中的每一个都是短暂的，因此我们不需要并行执行许多线程来处理它们。这种模式也简化了执
行通过使服务器程序员写代码的服务器好像有只有一个连接在任何特定时间正在处理当中。通过杀死相应的
线程并让垃圾收集器完成工作，它还使我们能够自由清理失败的或“僵尸”连接及其相关资源。

关键的观察结果是，托管“传统” Web应用程序的HTTP服务器具有 ññ 并发用户可能只需要处理很小一部分 ñ
ñ并行请求成功。对于我们尝试构建的交互式应用程序的类型，ññ 用户几乎肯定会要求应用程序至少维护 ñ
ñ 一次并行连接。

保持长期连接的结果是我们需要：

  • 一个足够“便宜”线程的平台，我们可以一次使用大量线程。
  • 一种服务器体系结构，可以通过一个线程处理许多连接。

有诸如Racket，Erlang和Haskell之类的编程环境提供了类似线程的结构，这些结构“轻量级”足以考虑第一
个选项。这种方法要求程序员明确处理同步问题，而同步问题在长时间打开连接并可能都在争夺相似资源的
系统中将更加普遍。具体来说，如果我们有多个用户同时共享的某种中央数据，则需要以某种方式协调对该
数据的读写。

如果我们没有便宜的线程可供使用，或者我们不愿意使用显式同步，则必须考虑让一个线程处理许多连接。
^2在此模型中，我们的单线程将一次处理多个请求的微小“切片”，并尽可能高效地在它们之间进行切换。这
种系统架构模式通常被称为事件驱动或基于事件。^3

由于我们仅管理一个线程，因此不必担心保护共享资源不被同时访问。但是，在此模型中，我们确实有一个
独特的问题。由于我们的单线程可同时处理所有进行中的请求，因此我们必须确保它永不阻塞。阻止任何连
接会阻止整个服务器处理任何其他请求。如果无法进一步为当前客户提供服务，我们必须能够转移到另一位
客户，并且我们必须能够以一种不会浪费迄今为止完成工作的方式来做到这一点。^4

对于程序员来说，显式地告诉线程停止工作是不常见的，但是许多常见的操作都有被阻塞的风险。因为线程
是如此普遍，并且对异步性的推理对程序员来说是沉重的负担，所以许多语言及其框架都认为对I / O进行
阻塞是理想的特性。这使得很容易在偶然的地方阻塞。幸运的是，Common Lisp确实为我们提供了最少的异
步I / O原语集，我们可以在这些基础上构建它们。

建筑决策

既然我们已经研究了这个问题的背景，那么我们已经到了需要就所构建的东西做出明智决定的时候了。

在我开始考虑这个项目的时候，Common Lisp没有完整的绿线程实现，并且标准的可移植线程库也没有“真正
便宜”的资格。这些选择要么等于选择其他语言，要么就是出于我的目的构建事件驱动的Web服务器。我选择
了后者。

除了服务器体系结构之外，我们还需要选择使用三种服务器推送方法中的哪一种。我们正在考虑（一种交互
式多人棋盘游戏）用例需要频繁更新到每个客户端，但相对稀疏的请求，从每一个客户，这符合上证所的方
法来推动更新，所以我们这一去。

现在，我们已经激励了架构决策，并决定了一种用于模拟客户端和服务器之间的双向通信的机制，让我们开
始构建Web框架。我们将首先建立一个相对“沉默”服务器开始，然后我们将其扩展到Web应用程序框架，它可
以让我们专注于什么我们重交互式程序需要做的，而不是如何它是做什么的。

构建事件驱动的Web服务器

大多数使用单个进程管理并发工作流的程序都使用一种称为事件循环的模式。让我们看看我们的Web服务器
的事件循环是什么样的。

事件循环

我们的事件循环需要：

  • 监听传入的连接；
  • 处理现有连接上的所有新握手或传入数据；
  • 清理被意外杀死（例如通过中断）的悬空套接字

(defmethod start ((port integer))
  (let ((server (socket-listen
         usocket:*wildcard-host* port
         :reuse-address t
         :element-type 'octet))
    (conns (make-hash-table)))
    (unwind-protect
     (loop (loop for ready
          in (wait-for-input
              (cons server (alexandria:hash-table-keys conns))
              :ready-only t)
          do (process-ready ready conns)))
      (loop for c being the hash-keys of conns
     do (loop while (socket-close c)))
      (loop while (socket-close server)))))

如果您以前没有编写Common Lisp程序，则此代码块需要一些解释。我们在这里写的是一个方法定义。尽管
Lisp通常被称为功能语言，但它也有自己的面向对象编程的系统，即“ Common Lisp对象系统”，通常缩写为
“ CLOS”。^5

CLOS和通用函数

在CLOS中，我们不关注类和方法，而是编写作为方法集合实现的泛型函数。在此模型中，方法不属于类，它
们专门研究类型。^6在我们刚写的方法是其中的参数一元法是专门的类型。这意味着我们可以有几种实现方
式，其中where 的类型不同，并且运行时将根据调用when 的类型来选择使用哪种实现。^
startportintegerstartportportstart

更一般而言，方法可以专注于多个参数。method调用a 时，运行时：

  • 分派其参数的类型，以确定应该运行哪个方法主体，以及
  • 运行适当的功能。

处理插座

我们将在中看到另一个通用函数process-ready，该函数在我们的事件循环中被更早调用。它使用两种方法
之一处理就绪的套接字，具体取决于我们处理的套接字的类型。

我们关注的两种类型是stream-usocket，它表示将发出请求并期望发送回一些数据的客户端套接字，以及
stream-server-usocket，它表示本地TCP侦听器，它将具有新的客户端连接供我们处理。。

如果a stream-server-socket为ready，则意味着有一个新的客户端套接字正在等待开始对话。我们调用
socket-accept接受连接，然后将结果放入连接表中，以便事件循环可以开始与其他循环一起处理它。

(defmethod process-ready ((ready stream-server-usocket) (conns hash-table))
  (setf (gethash (socket-accept ready :element-type 'octet) conns) nil))

当a stream-usocket为时ready，表示它已准备好一些字节供我们读取。（另一方也有可能终止了连接。）

(defmethod process-ready ((ready stream-usocket) (conns hash-table))
  (let ((buf (or (gethash ready conns)
         (setf (gethash ready conns)
               (make-instance 'buffer :bi-stream (flex-stream ready))))))
    (if (eq :eof (buffer! buf))
    (ignore-errors
      (remhash ready conns)
      (socket-close ready))
    (let ((too-big?
           (> (total-buffered buf)
          +max-request-size+))
          (too-old?
           (> (- (get-universal-time) (started buf))
          +max-request-age+))
          (too-needy?
           (> (tries buf)
          +max-buffer-tries+)))
      (cond (too-big?
         (error! +413+ ready)
         (remhash ready conns))
        ((or too-old? too-needy?)
         (error! +400+ ready)
         (remhash ready conns))
        ((and (request buf) (zerop (expecting buf)))
         (remhash ready conns)
         (when (contents buf)
           (setf (parameters (request buf))
             (nconc (parse buf) (parameters (request buf)))))
         (handler-case
             (handle-request ready (request buf))
           (http-assertion-error () (error! +400+ ready))
           ((and (not warning)
             (not simple-error)) (e)
             (error! +500+ ready e))))
        (t
         (setf (contents buf) nil)))))))

这比第一种情况涉及更多。我们：

 1. 获取与此套接字关联的缓冲区，或者如果尚不存在，则创建它；
 2. 将输出读取到该缓冲区中，这发生在对buffer!; 的调用中
 3. 如果读了我们的:eof，对方就挂了，所以我们丢弃的插座和它的缓冲;
 4. 否则，我们检查，如果缓冲区的一个complete?，too-big?，too-old?或too-needy?。如果是这样，我
    们将其从连接表中删除并返回适当的HTTP响应。

这是我们第一次在事件循环中看到I / O。在“ 传统HTTP Server体系结构”的讨论中，我们提到事件驱动系
统中的I / O必须非常小心，因为我们可能会意外阻塞单个线程。那么，我们在这里做什么以确保不会发生
这种情况？我们必须探索我们的实现，buffer!以确切地了解其工作原理。

处理连接而不阻塞

库函数的无阻塞处理方法的基础是库函数read-char-no-hang，nil当对没有可用数据的流进行调用时，库函
数会立即返回。在有要读取的数据的地方，我们使用缓冲区存储此连接的中间输入。

(defmethod buffer! ((buffer buffer))
  (handler-case
      (let ((stream (bi-stream buffer)))
        (incf (tries buffer))
        (loop for char = (read-char-no-hang stream) until (null char)
           do (push char (contents buffer))
           do (incf (total-buffered buffer))
           when (request buffer) do (decf (expecting buffer))
           when (line-terminated? (contents buffer))
           do (multiple-value-bind (parsed expecting) (parse buffer)
            (setf (request buffer) parsed
                  (expecting buffer) expecting)
            (return char))
           when (> (total-buffered buffer) +max-request-size+) return char
           finally (return char)))
    (error () :eof)))

在buffer!上调用buffer时：

  • 增加tries计数，以便我们可以逐出process-ready；
  • 循环从输入流中读取字符，并且
  • 如果已读取所有可用输入，则返回读取的最后一个字符。

它还跟踪任何\r\n\r\n序列，以便我们以后可以检测到完整的请求。最后，如果导致任何错误，它将返回一
个:eofto信号，process-ready该信号应放弃此连接。

该buffer类型是CLOS 类。CLOS中的类让我们定义一个名为的字段的类型slots。我们没有buffer在类定义上
看到与之相关的行为，因为（正如我们已经了解的那样），我们使用诸如的通用函数来做到这一点buffer!
。

defclass确实允许我们指定getter / setter（readers / accessors）和slot初始化程序；:initform指定
默认值，同时:initarg标识一个挂钩，调用者make-instance可以使用该挂钩提供默认值。

(defclass buffer ()
  ((tries :accessor tries :initform 0)
   (contents :accessor contents :initform nil)
   (bi-stream :reader bi-stream :initarg :bi-stream)
   (total-buffered :accessor total-buffered :initform 0)
   (started :reader started :initform (get-universal-time))
   (request :accessor request :initform nil)
   (expecting :accessor expecting :initform 0)))

我们buffer班有七个席位：

  • tries，它会记录我们尝试读入此缓冲区的次数
  • contents，其中包含我们到目前为止已阅读的内容
  • bi-stream，这是我前面提到的一些常见的Lisp特定的，非阻塞的I / O烦恼
  • total-buffered，这是到目前为止我们已经阅读的字符数
  • started，这是一个时间戳，告诉我们何时创建此缓冲区
  • request，最终将包含我们根据缓冲数据构造的请求
  • expecting，这将表明在缓冲请求标头后，我们期望再增加多少个字符（如果有）

口译请求

 现在，我们已经了解了如何从池中存储的数据中增量组合完整的请求，当我们准备好处理完整的请求时会
发生什么？这发生在方法中handle-request。

(defmethod handle-request ((socket usocket) (req request))
  (aif (lookup (resource req) *handlers*)
       (funcall it socket (parameters req))
       (error! +404+ socket)))

此方法增加了另一层错误处理，因此，如果请求是旧的，大的或有需要的，我们可以发送400响应以指示客
户端为我们提供了一些不良或缓慢的数据。但是，如果此处发生任何其他错误，那是因为程序员在定义处理
程序时犯了一个错误，应将其视为500错误。这将通知客户端，由于其合法请求，服务器上出现了问题。

如果请求的格式正确，则我们将在*handlers*表中查找请求的资源，这很简单。如果找到一个，我们
funcall it将传递客户端socket以及已解析的请求参数。如果*handlers*表中没有匹配的处理程序，我们将
发送404错误。处理程序系统将成为我们成熟的Web框架的一部分，我们将在后面的部分中进行讨论。

但是，我们仍然没有看到如何从我们的缓冲区之一解析和解释请求。接下来让我们看一下：

(defmethod parse ((buf buffer))
  (let ((str (coerce (reverse (contents buf)) 'string)))
    (if (request buf)
        (parse-params str)
        (parse str))))

此高级方法委派专门parse用于纯字符串或parse-params将缓冲区内容解释为HTTP参数的专业化。这些调用
取决于我们已经处理了多少请求；parse当我们已经在中request保存了部分内容时，就会发生最后的情况
buffer，此时，我们只希望解析请求正文。

(defmethod parse ((str string))
  (let ((lines (split "\\r?\\n" str)))
    (destructuring-bind (req-type path http-version) (split " " (pop lines))
      (declare (ignore req-type))
      (assert-http (string= http-version "HTTP/1.1"))
      (let* ((path-pieces (split "\\?" path))
         (resource (first path-pieces))
         (parameters (second path-pieces))
         (req (make-instance 'request :resource resource)))
    (loop
       for header = (pop lines)
       for (name value) = (split ": " header)
       until (null name)
       do (push (cons (->keyword name) value) (headers req)))
    (setf (parameters req) (parse-params parameters))
    req))))

(defmethod parse-params ((params null)) nil)

(defmethod parse-params ((params string))
  (loop for pair in (split "&" params)
     for (name val) = (split "=" pair)
     collect (cons (->keyword name) (or val ""))))

在parse专营的方法中string，我们将内容转换为可用的片段。我们对字符串执行此操作，而不是直接使用
缓冲区，因为这样可以更轻松地在解释器或REPL等环境中测试实际的解析代码。

解析过程为：

 1. 拆分"\\r?\\n"。
 2. 拆分的其上的第一行" "，以获得请求类型（POST，GET等）/ URI路径/ HTTP的版本。
 3. 断言我们正在处理一个HTTP/1.1请求。
 4. 在上分割URI路径"?"，这为我们提供了与任何GET参数分开的纯资源。
 5. request使用资源创建一个新实例。
 6. request用每个拆分的标题行填充该实例。
 7. 将requests参数设置为解析GET参数的结果。

正如您现在所期望的那样，它request是CLOS类的实例：

    (defclass request ()
      ((resource :accessor resource :initarg :resource)
       (headers :accessor headers :initarg :headers :initform nil)
       (parameters :accessor parameters :initarg :parameters :initform nil)))

现在，我们已经了解了客户如何发送请求，并由我们的服务器解释和处理请求。作为核心服务器接口的一部
分，我们必须实现的最后一件事是能够将响应写回到客户端。

渲染响应

在讨论渲染响应之前，我们必须考虑可能要返回给客户的响应有两种。第一个是“正常” HTTP响应，其中包
含HTTP标头和正文。我们用response该类的实例表示这些类型的响应：

(defclass response ()
  ((content-type
    :accessor content-type :initform "text/html" :initarg :content-type)
   (charset
    :accessor charset :initform "utf-8")
   (response-code
    :accessor response-code :initform "200 OK" :initarg :response-code)
   (keep-alive?
    :accessor keep-alive? :initform nil :initarg :keep-alive?)
   (body
    :accessor body :initform nil :initarg :body)))

第二个是SSE消息，我们将使用它向客户发送增量更新。

(defclass sse ()
  ((id :reader id :initarg :id :initform nil)
   (event :reader event :initarg :event :initform nil)
   (retry :reader retry :initarg :retry :initform nil)
   (data :reader data :initarg :data)))

每当我们收到完整的HTTP请求时，我们都会发送HTTP响应；但是，我们如何知道在没有原始客户端请求的情
况下何时何地发送SSE消息？

一个简单的解决方案是注册通道^7，我们将socket根据需要订阅。

(defparameter *channels* (make-hash-table))

(defmethod subscribe! ((channel symbol) (sock usocket))
  (push sock (gethash channel *channels*))
  nil)

然后，我们可以publish!在这些频道可用后立即通知它们。

(defmethod publish! ((channel symbol) (message string))
  (awhen (gethash channel *channels*)
     (setf (gethash channel *channels*)
           (loop with msg = (make-instance 'sse :data message)
          for sock in it
          when (ignore-errors
             (write! msg sock)
             (force-output (socket-stream sock))
             sock)
          collect it))))

在中publish!，我们调用write!实际将an写入sse套接字。我们还需要write!on response的特殊化来编写完
整的HTTP响应。让我们首先处理HTTP情况。

(defmethod write! ((res response) (socket usocket))
  (handler-case
      (with-timeout (.2)
    (let ((stream (flex-stream socket)))
      (flet ((write-ln (&rest sequences)
           (mapc (lambda (seq) (write-sequence seq stream)) sequences)
           (crlf stream)))
        (write-ln "HTTP/1.1 " (response-code res))
        (write-ln
         "Content-Type: " (content-type res) "; charset=" (charset res))
        (write-ln "Cache-Control: no-cache, no-store, must-revalidate")
        (when (keep-alive? res)
          (write-ln "Connection: keep-alive")
          (write-ln "Expires: Thu, 01 Jan 1970 00:00:01 GMT"))
        (awhen (body res)
          (write-ln "Content-Length: " (write-to-string (length it)))
          (crlf stream)
          (write-ln it))
        (values))))
    (trivial-timeout:timeout-error ()
      (values))))

此版本的write!带有response和的usocket名称sock，并将内容写入由提供的流sock。我们在本地定义
write-ln需要一些序列的函数，然后将其写到流中crlf。这是为了提高可读性；我们可以直接调用
write-sequence/ crlf。

请注意，我们再次执行“必须阻止”的操作。尽管写入可能会被缓冲，并且阻塞的可能性要比读取低，但我们
仍然不希望我们的服务器在发生问题时停止运行。如果写入花费的时间超过0.2秒^8，我们将继续前进（抛
出当前套接字），而不是再等待了。

写出在SSE概念上类似于写出response：

(defmethod write! ((res sse) (socket usocket))
  (let ((stream (flex-stream socket)))
    (handler-case
    (with-timeout (.2)
      (format
       stream "~@[id: ~a~%~]~@[event: ~a~%~]~@[retry: ~a~%~]data: ~a~%~%"
       (id res) (event res) (retry res) (data res)))
      (trivial-timeout:timeout-error ()
        (values)))))

这比处理完整的HTTP响应要简单，因为SSE消息标准没有指定CRLF行尾，因此我们可以通过一个format调用
来解决问题。该~@[... ~]块是有条件的指令，这使我们能够从容地处理nil槽。例如，如果(id res)为
non-nil，我们将输出id: <the id here>，否则我们将完全忽略该指令。增量更新的有效负载data是唯一需
要的插槽sse，因此我们可以将其包括在内而不必担心nil。再说一次，我们等待的时间不会太长。0.2秒后
，如果写入尚未完成，我们将超时并继续进行下一个操作。

错误回应

到目前为止，我们对请求/响应周期的处理尚未涵盖发生错误时发生的情况。具体来说，我们在此error!函
数中使用了该函数，handle-request而process-ready没有描述它的作用。

(define-condition http-assertion-error (error)
  ((assertion :initarg :assertion :initform nil :reader assertion))
  (:report (lambda (condition stream)
         (format stream "Failed assertions '~s'"
             (assertion condition)))))

define-condition在Common Lisp中创建新的错误类。在这种情况下，我们正在定义一个HTTP断言错误，并
指出它将特别需要知道它所作用的实际断言，以及一种将自身输出到流中的方法。在其他语言中，您将其称
为方法。在这里，它是一个恰好是类的插槽值的函数。

我们如何向客户表示错误？让我们定义4xx和5xx-class HTTP错误，我们将使用频繁：

(defparameter +404+
  (make-instance
   'response :response-code "404 Not Found"
   :content-type "text/plain"
   :body "Resource not found..."))

(defparameter +400+
  (make-instance
   'response :response-code "400 Bad Request"
   :content-type "text/plain"
   :body "Malformed, or slow HTTP request..."))

(defparameter +413+
  (make-instance
   'response :response-code "413 Request Entity Too Large"
   :content-type "text/plain"
   :body "Your request is too long..."))

(defparameter +500+
  (make-instance
   'response :response-code "500 Internal Server Error"
   :content-type "text/plain"
   :body "Something went wrong on our end..."))

现在我们可以看到error!它的作用：

(defmethod error! ((err response) (sock usocket) &optional instance)
  (declare (ignorable instance))
  (ignore-errors
    (write! err sock)
    (socket-close sock)))

它接受一个错误响应和一个套接字，将响应写入套接字并关闭它（忽略错误，以防另一端已断开连接）。
instance这里的参数用于记录/调试目的。

除此之外，我们还有一个事件驱动的Web服务器，可以响应HTTP请求或发送SSE消息，并带有错误处理功能！

将服务器扩展到Web框架

现在，我们已经构建了功能合理的Web服务器，该服务器可以将请求，响应和消息往返于客户端。由该服务
器托管的任何Web应用程序的实际工作都是通过委派处理程序功能完成的，这些功能已在“ 解释请求”中进行
了介绍，但未指定。

服务器与托管应用程序之间的接口很重要，因为它指示应用程序程序员使用我们的基础结构的难易程度。理
想情况下，我们的处理程序接口会将参数从请求映射到执行实际工作的函数：

(define-handler (source :is-stream? nil) (room)
  (subscribe! (intern room :keyword) sock))

(define-handler (send-message) (room name message)
  (publish! (intern room :keyword)
        (encode-json-to-string
         `((:name . ,name) (:message . ,message)))))

(define-handler (index) ()
  (with-html-output-to-string (s nil :prologue t :indent t)
    (:html
     (:head (:script
         :type "text/javascript"
         :src "/static/js/interface.js"))
     (:body (:div :id "messages")
        (:textarea :id "input")
        (:button :id "send" "Send")))))

编写House时，我想到的一个担忧是，就像开放给更大互联网的任何应用程序一样，它将处理来自不受信任
的客户的请求。这将是很好能够具体说了什么类型的每个请求应通过提供小包含数据的架构描述数据。我们
之前的处理程序列表将如下所示：

(defun len-between (min thing max)
  (>= max (length thing) min))

(define-handler (source :is-stream? nil)
    ((room :string (len-between 0 room 16)))
  (subscribe! (intern room :keyword) sock))

(define-handler (send-message)
    ((room :string (len-between 0 room 16))
     (name :string (len-between 1 name 64))
     (message :string (len-between 5 message 256)))
  (publish! (intern room :keyword)
        (encode-json-to-string
         `((:name . ,name) (:message . ,message)))))

(define-handler (index) ()
  (with-html-output-to-string (s nil :prologue t :indent t)
    (:html
     (:head (:script
         :type "text/javascript"
         :src "/static/js/interface.js"))
     (:body (:div :id "messages")
        (:textarea :id "input")
        (:button :id "send" "Send")))))

虽然我们仍与Lisp代码的工作，这个界面开始看起来几乎像一个说明性语言，在我们国家是什么，我们希望
我们的处理程序，以验证没有想太多关于如何他们会做到这一点。我们正在做的是为处理程序功能构建特定
于域的语言（DSL）。也就是说，我们正在创建一种特定的约定和语法，使我们可以准确地准确表达我们希
望处理程序验证的内容。Lisp程序员经常使用这种构建小型语言来解决当前问题的方法，这是一种有用的技
术，可以应用于其他编程语言。

处理程序的DSL

现在我们有了一个松散的规范，说明我们希望处理程序DSL的外观如何实现？也就是说，当我们打电话时，
我们期望发生什么define-handler？让我们考虑send-message上面的定义：

(define-handler (send-message)
    ((room :string (len-between 0 room 16))
     (name :string (len-between 1 name 64))
     (message :string (len-between 5 message 256)))
  (publish! (intern room :keyword)
        (encode-json-to-string
         `((:name . ,name) (:message . ,message)))))

我们想define-handler在这里做的是：

 1. 将操作绑定(publish! ...)到/send-message处理程序表中的URI 。
 2. 对此URI发出请求时：
      □ 确保HTTP参数room，name并且message都包括在内。
      □ 验证room字符串长度不超过16个字符，字符串长度name在1到64个字符之间（包括端点），并且
        message字符串长度在5到256个字符之间（也包括端点）。
 3. 返回响应后，关闭通道。

虽然我们可以写Lisp函数做所有这些事情，然后手动组装件自己，更常见的做法是使用Lisp的工厂叫macros
到生成的Lisp代码为我们。这使我们可以简洁地表达我们希望DSL进行的工作，而无需维护大量代码即可完
成。您可以将宏视为“可执行模板”，它将在运行时扩展为Lisp代码。

这是我们的define-handler宏^9：

(defmacro define-handler
    ((name &key (is-stream? t) (content-type "text/html")) (&rest args)
     &body body)
  (if is-stream?
      `(bind-handler
    ,name (make-closing-handler
           (:content-type ,content-type)
           ,args ,@body))
      `(bind-handler
    ,name (make-stream-handler ,args ,@body))))

它委托给其他三个宏（bind-handler，make-closing-handler，make-stream-handler）我们将在稍后确定
。make-closing-handler将为整个HTTP请求/响应周期创建一个处理程序；make-stream-handler而是处理
SSE消息。谓词is-stream?为我们区分了这些情况。反引号和逗号是特定于宏的运算符，我们可以使用它们
在代码中“挖洞”，而在实际使用时，这些值将由Lisp代码中指定的值填充define-handler。

请注意，我们的宏与我们要执行的操作的规范非常接近define-handler：如果我们要编写一系列Lisp函数来
完成所有这些操作，则通过检查很难识别代码的意图。

扩展处理程序

让我们逐步扩展send-message处理程序，以便更好地了解Lisp为我们“扩展”宏时实际发生的情况。我们将使
用SLIME Emacs模式下的宏扩展功能来执行此操作。调用macro-expander上define-handler将由一个“级别”
扩大我们的宏观，让我们的辅助宏在他们仍然浓缩的形式：

(BIND-HANDLER
 SEND-MESSAGE
 (MAKE-CLOSING-HANDLER
  (:CONTENT-TYPE "text/html")
  ((ROOM :STRING (LEN-BETWEEN 0 ROOM 16))
   (NAME :STRING (LEN-BETWEEN 1 NAME 64))
   (MESSAGE :STRING (LEN-BETWEEN 5 MESSAGE 256)))
  (PUBLISH! (INTERN ROOM :KEYWORD)
        (ENCODE-JSON-TO-STRING
         `((:NAME ,@NAME) (:MESSAGE ,@MESSAGE))))))

通过将send-message特定的代码替换为处理程序模板，宏已经为我们节省了一些键入时间。bind-handler是
另一个将URI映射到我们的处理程序表上的处理程序函数的宏；由于它现在是我们扩展的基础，因此在进一
步扩展之前，让我们看看它是如何定义的。

(defmacro bind-handler (name handler)
  (assert (symbolp name) nil "`name` must be a symbol")
  (let ((uri (if (eq name 'root) "/" (format nil "/~(~a~)" name))))
    `(progn
       (when (gethash ,uri *handlers*)
     (warn ,(format nil "Redefining handler '~a'" uri)))
       (setf (gethash ,uri *handlers*) ,handler))))

绑定发生在最后一行：(setf (gethash ,uri *handlers*) ,handler)，这是Common Lisp中的哈希表分配的
样子（以逗号为模，这是宏的一部分）。请注意，assert不在引号区域内，这意味着它将在调用宏后立即运
行，而不是在评估结果时运行。

当我们进一步扩展send-message define-handler上述扩展时，我们得到：

(PROGN
  (WHEN (GETHASH "/send-message" *HANDLERS*)
    (WARN "Redefining handler '/send-message'"))
  (SETF (GETHASH "/send-message" *HANDLERS*)
    (MAKE-CLOSING-HANDLER
     (:CONTENT-TYPE "text/html")
     ((ROOM :STRING (LEN-BETWEEN 0 ROOM 16))
      (NAME :STRING (LEN-BETWEEN 1 NAME 64))
      (MESSAGE :STRING (LEN-BETWEEN 5 MESSAGE 256)))
     (PUBLISH! (INTERN ROOM :KEYWORD)
           (ENCODE-JSON-TO-STRING
            `((:NAME ,@NAME) (:MESSAGE ,@MESSAGE)))))))

这看起来开始更像是一个自定义实现，如果我们自己编写的话，我们将编写它来处理从URI向处理程序功能
的请求。但是我们没有必要！

我们仍然需要make-closing-handler继续扩展。这是它的定义：

(defmacro make-closing-handler
    ((&key (content-type "text/html")) (&rest args) &body body)
  `(lambda (sock parameters)
     (declare (ignorable parameters))
     ,(arguments
       args
       `(let ((res (make-instance
            'response
            :content-type ,content-type
            :body (progn ,@body))))
      (write! res sock)
      (socket-close sock)))))

因此，制作关闭处理程序涉及到制作lambda，这就是您在Common Lisp中称为匿名函数的内容。我们还设置
了一个内部作用域，该作用域使我们传入response的body参数脱颖而出，write!对请求的套接字执行a ，然
后将其关闭。剩下的问题是什么arguments？

(defun arguments (args body)
  (loop with res = body
     for arg in args
     do (match arg
     ((guard arg-sym (symbolp arg-sym))
      (setf res `(let ((,arg-sym ,(arg-exp arg-sym))) ,res)))
     ((list* arg-sym type restrictions)
      (setf res
        (let ((sym (or (type-expression
                (arg-exp arg-sym)
                type restrictions)
                   (arg-exp arg-sym))))
          `(let ((,arg-sym ,sym))
             ,@(awhen (type-assertion arg-sym type restrictions)
             `((assert-http ,it)))
             ,res)))))
     finally (return res)))

欢迎来到困难的部分。arguments将我们向处理程序注册的验证器变成解析尝试和断言的树。
type-expression，arg-exp和type-assertion用于实现和实施“类型系统”，用于我们在响应中期望的各种数
据；我们将在HTTP“ Types”中讨论它们。与此结合使用make-closing-handler将实现我们在此处编写的验证
规则：

(define-handler (send-message)
    ((room :string (>= 16 (length room)))
     (name :string (>= 64 (length name) 1))
     (message :string (>= 256 (length message) 5)))
  (publish! (intern room :keyword)
        (encode-json-to-string
         `((:name . ,name) (:message . ,message)))))

...作为验证请求所需的“展开”检查序列：

(LAMBDA (SOCK #:COOKIE?1111 SESSION PARAMETERS)
  (DECLARE (IGNORABLE SESSION PARAMETERS))
  (LET ((ROOM (AIF (CDR (ASSOC :ROOM PARAMETERS))
           (URI-DECODE IT)
           (ERROR (MAKE-INSTANCE
               'HTTP-ASSERTION-ERROR
               :ASSERTION 'ROOM)))))
    (ASSERT-HTTP (>= 16 (LENGTH ROOM)))
    (LET ((NAME (AIF (CDR (ASSOC :NAME PARAMETERS))
             (URI-DECODE IT)
             (ERROR (MAKE-INSTANCE
                 'HTTP-ASSERTION-ERROR
                 :ASSERTION 'NAME)))))
      (ASSERT-HTTP (>= 64 (LENGTH NAME) 1))
      (LET ((MESSAGE (AIF (CDR (ASSOC :MESSAGE PARAMETERS))
              (URI-DECODE IT)
              (ERROR (MAKE-INSTANCE
                  'HTTP-ASSERTION-ERROR
                  :ASSERTION 'MESSAGE)))))
    (ASSERT-HTTP (>= 256 (LENGTH MESSAGE) 5))
    (LET ((RES (MAKE-INSTANCE
            'RESPONSE :CONTENT-TYPE "text/html"
            :COOKIE (UNLESS #:COOKIE?1111
                  (TOKEN SESSION))
            :BODY (PROGN
                (PUBLISH!
                 (INTERN ROOM :KEYWORD)
                 (ENCODE-JSON-TO-STRING
                  `((:NAME ,@NAME)
                (:MESSAGE ,@MESSAGE))))))))
      (WRITE! RES SOCK)
      (SOCKET-CLOSE SOCK))))))

这使我们获得了完整HTTP请求/响应周期所需的验证。那我们的SSE呢？make-stream-handler做与相同的基
本操作make-closing-handler，除了它写一个SSE而不是一个RESPONSE，然后调用force-output代替，
socket-close因为我们想通过连接刷新数据而不关闭它：

(defmacro make-stream-handler ((&rest args) &body body)
  `(lambda (sock parameters)
     (declare (ignorable parameters))
     ,(arguments
       args
       `(let ((res (progn ,@body)))
      (write! (make-instance
           'response
           :keep-alive? t
           :content-type "text/event-stream")
          sock)
      (write!
       (make-instance 'sse :data (or res "Listening..."))
       sock)
      (force-output
       (socket-stream sock))))))

(defmacro assert-http (assertion)
  `(unless ,assertion
     (error (make-instance
         'http-assertion-error
         :assertion ',assertion))))

assert-http是一个宏，用于创建错误情况下所需的样板代码。它扩展为检查给定的断言，
http-assertion-error如果失败则抛出，并在该事件中打包原始断言。

(defmacro assert-http (assertion)
  `(unless ,assertion
     (error (make-instance
         'http-assertion-error
         :assertion ',assertion))))

HTTP“类型”

 

在上一节中，我们简要的三个表达式感动的是我们用来实现我们的HTTP类型的验证系统：arg-exp，
type-expression和type-assertion。一旦了解了这些内容，我们的框架中就不会剩下任何魔术。我们将从
简单的第一开始。

arg-exp

arg-exp接受一个符号并创建一个aif用于检查参数是否存在的表达式。

(defun arg-exp (arg-sym)
  `(aif (cdr (assoc ,(->keyword arg-sym) parameters))
    (uri-decode it)
    (error (make-instance
        'http-assertion-error
        :assertion ',arg-sym))))

评估arg-exp符号看起来像：

HOUSE> (arg-exp 'room)
(AIF (CDR (ASSOC :ROOM PARAMETERS))
     (URI-DECODE IT)
     (ERROR (MAKE-INSTANCE
         'HTTP-ASSERTION-ERROR
         :ASSERTION 'ROOM)))
HOUSE>

我们一直在使用像这样的表格，aif并且awhen不了解它们是如何工作的，所以让我们花一些时间来研究它们
。

回想一下，Lisp代码本身被表示为树。这就是括号的含义；他们向我们展示了树叶和树枝如何配合在一起。
如果回到上一节中的操作，则make-closing-handler调用一个函数arguments来生成其正在构建的Lisp树的
一部分，该函数又调用一些操纵树的辅助函数，包括arg-exp，以生成其返回值。

也就是说，我们构建了一个小型系统，该系统以Lisp表达式作为输入，并生成不同的Lisp表达式作为输出。
概念化的最简单方法可能是专门针对当前问题的简单Common-Lisp-to-Common-Lisp编译器。

这种编译器的一种广泛使用的分类是回指宏。该术语来自照应的语言学概念，即使用一个单词代替其前面的
一组单词。aif并且awhen是照应性宏，它们是我倾向于经常使用的唯一宏。anaphora软件包中还有许多可用
的工具。

据我所知，隐喻宏最早是由Paul Graham在OnLisp章节中定义的。他给出的用例是您要进行某种昂贵或半昂
贵的检查，然后有条件地对结果进行一些处理的情况。在上面的上下文中，我们aif用来检查alist遍历的结
果。

(aif (cdr (assoc :room parameters))
     (uri-decode it)
     (error (make-instance
         'http-assertion-error
         :assertion 'room)))

这需要在关联列表cdr中查找符号。如果返回一个非nil值，则返回一个错误类型
。:roomparametersuri-decodehttp-assertion-error

换句话说，以上等同于：

(let ((it (cdr (assoc :room parameters))))
  (if it
      (uri-decode it)
      (error (make-instance
          'http-assertion-error
          :assertion 'room))))

Maybe在这种情况下，像Haskell这样的强类型函数语言通常使用类型。在Common Lisp中，我们捕获it扩展
中的符号作为检查结果的名称。

了解了这一点，我们应该能够看到arg-exp正在生成最终要评估的特定的，重复的代码树。在这种情况下，
用于检查处理程序的中是否存在给定参数的部分parameters。现在，让我们继续...

类型表达

(defgeneric type-expression (parameter type)
  (:documentation
   "A type-expression will tell the server
how to convert a parameter from a string to
a particular, necessary type."))
...
(defmethod type-expression (parameter type) nil)

这是一个通用函数，它会生成新的树结构（巧合Lisp代码），而不仅仅是一个函数。上面唯一告诉您的是，
默认情况下，a type-expression是NIL。也就是说，我们没有一个。如果遇到NIL，我们将使用的原始输出
arg-exp，但这并不能告诉我们最常见的情况。为了看到这一点，让我们看一个内置的（to :house）
define-http-type表达式。

(define-http-type (:integer)
    :type-expression `(parse-integer ,parameter :junk-allowed t)
    :type-assertion `(numberp ,parameter))

一个:integer是我们从一个做什么parameter用parse-integer。该junk-allowed参数parse-integer表明我
们不确定所提供的数据实际上是否可解析，因此我们需要确保返回的结果是整数。如果不是，我们会得到以
下行为：

HOUSE> (type-expression 'blah :integer)
(PARSE-INTEGER BLAH :JUNK-ALLOWED T)
HOUSE>

define-http-handler^10是我们框架的导出符号之一。这让我们的应用程序的程序员定义他们自己的类型，
以简化解析“建宏”，我们给他们的少数以上（:string，:integer，:keyword，:json，:list-of-keyword
和:list-of-integer）。

(defmacro define-http-type ((type) &key type-expression type-assertion)
  (with-gensyms (tp)
    `(let ((,tp ,type))
       ,@(when type-expression
      `((defmethod type-expression (parameter (type (eql ,tp)))
          ,type-expression)))
       ,@(when type-assertion
      `((defmethod type-assertion (parameter (type (eql ,tp)))
          ,type-assertion))))))

它通过为要定义的类型创建type-expression和type-assertion方法定义来工作。我们可以让我们框架的用
户手动完成此操作，而不会带来太多麻烦。然而，加入间接这个额外的水平给我们，框架程序员，自由地变
化如何类型不强迫我们的用户能够实现重新写自己的规格。这不仅仅是学术上的考虑；首次构建时，我亲自
对系统的这一部分进行了重大更改，并且很高兴发现我对依赖于该系统的应用程序所做的编辑很少。

让我们看一下该整数定义的扩展，以详细了解其工作原理：

(LET ((#:TP1288 :INTEGER))
  (DEFMETHOD TYPE-EXPRESSION (PARAMETER (TYPE (EQL #:TP1288)))
    `(PARSE-INTEGER ,PARAMETER :JUNK-ALLOWED T))
  (DEFMETHOD TYPE-ASSERTION (PARAMETER (TYPE (EQL #:TP1288)))
    `(NUMBERP ,PARAMETER)))

就像我们说的那样，它并没有减少太多代码，但是确实避免了我们需要关心那些方法的特定参数是什么，甚
至根本不是它们。

类型断言

现在，我们可以定义类型了，让我们看一下如何type-assertion用来验证解析是否满足我们的要求。它也需
要一个互补的形式defgeneric/ defmethod对就像type-expression：

(defgeneric type-assertion (parameter type)
  (:documentation
   "A lookup assertion is run on a parameter
immediately after conversion. Use it to restrict
 the space of a particular parameter."))
...
(defmethod type-assertion (parameter type) nil)

这是此输出的内容：

HOUSE> (type-assertion 'blah :integer)
(NUMBERP BLAH)
HOUSE>

在某些情况下，type-assertion无需执行任何操作。例如，由于HTTP参数是作为字符串提供给我们的，因此
我们的:string类型断言没有任何可验证的内容：

HOUSE> (type-assertion 'blah :string)
NIL
HOUSE>

现在都在一起了

我们做到了！我们在事件驱动的Web服务器实现之上构建了一个Web框架。我们的框架（和处理程序DSL）通
过以下方式定义新的应用程序：

  • 将URL映射到处理程序；
  • 定义处理程序以对请求执行类型安全和验证规则；
  • （可选）根据需要为处理程序指定新类型。

现在我们可以像这样描述我们的应用程序：

(defun len-between (min thing max)
  (>= max (length thing) min))

(define-handler (source :is-stream? nil)
    ((room :string (len-between 0 room 16)))
  (subscribe! (intern room :keyword) sock))

(define-handler (send-message)
    ((room :string (len-between 0 room 16))
     (name :string (len-between 1 name 64))
     (message :string (len-between 5 message 256)))
  (publish! (intern room :keyword)
        (encode-json-to-string
         `((:name . ,name) (:message . ,message)))))

(define-handler (index) ()
  (with-html-output-to-string (s nil :prologue t :indent t)
    (:html
     (:head (:script
         :type "text/javascript"
         :src "/static/js/interface.js"))
     (:body (:div :id "messages")
        (:textarea :id "input")
        (:button :id "send" "Send")))))

(start 4242)

一旦我们编写interface.js了提供客户端交互的文件，这将在端口上启动一个HTTP聊天服务器4242并侦听传
入的连接。

━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━

 1. 解决此问题的一种方法是强制客户端轮询服务器。也就是说，每个客户端都会定期向服务器发送请求，
    询问是否有任何更改。这可以用于简单的应用程序，但是在本章中，我们将重点介绍该模型停止工作时
    可用的解决方案。↩

 2. 我们可以考虑一个更通用的系统来处理 ññ 并发用户 中号中号线程的一些可配置值 中号中号; 在这个
    模型中，ññ连接所述被复用的跨中号中号线程。在本章中，我们将重点放在编写程序上中号中号固定为
    1；但是，这里获得的教训应该部分地适用于更一般的模型。↩

 3. 这种术语有点混乱，它起源于早期的操作系统研究。它指的是如何在多个并发进程之间进行通信。在基
    于线程的系统中，通信是通过同步资源（例如共享内存）完成的。在基于事件的系统中，流程通常通过
    队列进行通信，在该队列中，流程将发布描述其已完成或要完成的内容的项目，这些内容由我们的单执
    行线程维护。由于这些项目通常描述了期望的或过去的动作，因此将它们称为“事件”。↩

 4. 请参阅有关此问题的另一种观点。↩

 5. 根据与您交谈的对象，发音为“ kloss”，“ see-loss”或“ see-lows”。↩

 6. Julia编程语言采用类似的方法进行面向对象的编程。您可以在中了解更多有关它的信息。↩

 7. 我们在这里顺便介绍了一些新语法。这是我们声明可变变量的方式。它具有形式(defparameter <name>
    <value> <optional docstring>)。↩

 8. with-timeout在不同的Lisps上具有不同的实现。在某些环境中，它可能创建另一个线程或进程来监视
    调用它的线程或进程。虽然我们一次最多只能创建其中之一，但是执行每次写入是一项相对繁重的操作
    。我们可能要考虑在这些环境中使用替代方法。↩

 9. 我应该注意，下面的代码块对于Common Lisp是非常非常规的缩进。Arglist通常不会在多行中分解，并
    且通常与宏/函数名称保持在同一行。我必须遵守本书的行宽准则，但否则，宁愿在代码内容所指定的
    位置使用较长的行来自然折断。↩

10. 很难读取此宏，因为它会通过NIL使用,@可能的方式扩展使其变得易于阅读。↩

Google 翻译

原文

提供更好的翻译建议
━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━
